plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

// sensitive data read
def noKeyFound = '"NO_KEY_FOUND"'
def newsApiKey = noKeyFound
def sensitiveDataFile = file('../sensitive_data.txt')
Properties sensitiveData = new Properties()

if (sensitiveDataFile.canRead()) {
    // Read the sensitive data from file sensitive_data.txt
    sensitiveData.load(new FileInputStream(sensitiveDataFile))
    if (sensitiveData != null) {
        if (sensitiveData.containsKey('newsApiKey')) {
            newsApiKey = sensitiveData['newsApiKey']
        }
    }
}

android {
    compileSdk 31

    defaultConfig {
        applicationId "com.nfragiskatos.news_api_client"
        minSdk 26
        targetSdk 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "NEWS_API_KEY", newsApiKey
        resValue "string", "NEWS_API_KEY", newsApiKey

        buildConfigField "String", "NEWS_API_BASE_URL", "\"https://newsapi.org\""
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.1'

    // GSon
    implementation 'com.google.code.gson:gson:2.8.8'

    // Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.2"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.2"

    // retrofit + Gson converter
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation "com.squareup.retrofit2:converter-gson:2.9.0"

    // okhttp
    implementation "com.squareup.okhttp3:okhttp:4.9.0"

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    // mockwebserver
    testImplementation "com.squareup.okhttp3:mockwebserver:4.9.2"

    // truth library
    testImplementation "com.google.truth:truth:1.1.3"
}